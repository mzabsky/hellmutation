/// An element which allows the user to switch between multiple
/// [frames](ZF_Frame) by clicking on tabs.
///
/// Tabs should be added to the element via [`addTab`] after creation.
class HM_ZFTabs : HM_ZFElementContainer {
	private void config(
		double tabHeight, double tabLabelMargin, Font tabFont = NULL, double tabTextScale = 1.0,
		HM_ZFBoxTextures tabNormal = NULL, HM_ZFBoxTextures tabHover = NULL, HM_ZFBoxTextures tabActive = NULL
	) {
		self.setBoxes = false;

		setTabHeight(tabHeight);
		setTabLabelMargin(tabLabelMargin);
		setTabFont(tabFont);
		setTabTextScale(tabTextScale);
		setTabNormalTexture(tabNormal);
		setTabHoverTexture(tabHover);
		setTabActiveTexture(tabActive);
		setAlpha(1.0);

		self.setBoxes = true;

		setTabLabelBoxes();
	}

	/// Returns a newly created tabs element.
	///
	/// The position used is relative to whatever the element is packed into
	/// later.
	///
	/// The tabs section of the element will have height `tabHeight`, with tab
	/// buttons with `tabLabelMargin` pixels of space between the text and
	/// borders (horizontally).
	///
	/// If `tabFont` is `NULL`, then `smallfont` is used instead.
	///
	/// The background for inactive tabs is `tabNormal`, with the background
	/// for active tabs being `tabActive`. When an inactive tab is hovered, it
	/// will use `tabHover` instead.
	///
	/// After creation, [`addTab`] should be used to add tabs.
	static HM_ZFTabs create(
		Vector2 pos, Vector2 size,
		double tabHeight, double tabLabelMargin, Font tabFont = NULL, double tabTextScale = 1.0,
		HM_ZFBoxTextures tabNormal = NULL, HM_ZFBoxTextures tabHover = NULL, HM_ZFBoxTextures tabActive = NULL
	) {
		let ret = new("HM_ZFTabs");

		ret.setBox(pos, size);
		ret.curTab = new("HM_ZFRadioController");
		ret.config(tabHeight, tabLabelMargin, tabFont, tabTextScale, tabNormal, tabHover, tabActive);

		return ret;
	}

	private Array<HM_ZFRadioButton> tabLabels;
	private Array<HM_ZFFrame> tabFrames;
	private HM_ZFRadioController curTab;

	private double tabHeight;
	/// Returns the height of the tabs section.
	double getTabHeight() { return self.tabHeight; }
	/// Sets the height of the tabs section;
	void setTabHeight(double tabHeight) { self.tabHeight = tabHeight; setTabLabelBoxes(); }

	private double tabLabelMargin;
	/// Returns the horizontal margin between tab buttons and their boxes'
	/// borders.
	double getTabLabelMargin() { return self.tabLabelMargin; }
	/// Sets the horizontal margin between tab buttons and their boxes'
	/// borders.
	void setTabLabelMargin(double tabLabelMargin) { self.tabLabelMargin = tabLabelMargin; setTabLabelBoxes(); }

	private Font tabFont;
	/// Returns the font the tab buttons will use for drawing their text.
	Font getTabFont() { return self.tabFont; }
	/// Returns the font the tab buttons will use for drawing their text.
	///
	/// If this is `NULL`, then `smallfont` is used instead.
	void setTabFont(Font tabFont) {
		if (tabFont == NULL) {
			self.tabFont = smallfont;
		}
		else {
			self.tabFont = tabFont;
		}
		setTabLabelBoxes();
	}

	private double tabTextScale;
	/// Returns the scaling factor for the drawn tab button text.
	double getTabTextScale() { return self.tabTextScale; }
	/// Sets the scaling factor for the drawn tab button text.
	void setTabTextScale(double tabTextScale) { self.tabTextScale = tabTextScale; setTabLabelBoxes(); }

	private HM_ZFBoxTextures tabNormal;
	/// Returns the box textures used for an inactive, unhovered tab.
	HM_ZFBoxTextures getTabNormalTexture() { return self.tabNormal; }
	/// Sets the box textures used for an inactive, unhovered tab.
	void setTabNormalTexture(HM_ZFBoxTextures tabNormal) { self.tabNormal = tabNormal; setTabLabelBoxes(); }

	private HM_ZFBoxTextures tabHover;
	/// Returns the box textures used for an inactive, hovered tab.
	HM_ZFBoxTextures getTabHoverTexture() { return self.tabHover; }
	/// Sets the box textures used for an inactive, hovered tab.
	void setTabHoverTexture(HM_ZFBoxTextures tabHover) { self.tabHover = tabHover; setTabLabelBoxes(); }

	private HM_ZFBoxTextures tabActive;
	/// Returns the box textures used for an active tab.
	HM_ZFBoxTextures getTabActiveTexture() { return self.tabActive; }
	/// Sets the box textures used for an active tab.
	void setTabActiveTexture(HM_ZFBoxTextures tabActive) { self.tabActive = tabActive; setTabLabelBoxes(); }

	private uint tabFocus;

	private int lastTab;

	private bool setBoxes;
	private void setTabLabelBoxes() {
		if (setBoxes) {
			double curX = 0.0;
			for (int i = 0; i < tabLabels.size(); i++) {
				let l = tabLabels[i];
				l.setBox((curX, 0.0), (tabFont.stringWidth(l.getText()) * tabTextScale + 2.0 * tabLabelMargin, tabHeight));
				l.config(curTab, i, tabNormal, tabHover, tabActive, NULL, l.getText(), tabFont, tabTextScale);
				curX += l.box.size.x;
			}
		}
	}
	
	override void getFocusAABB(HM_ZFAABB box) {
		let label = tabLabels[tabFocus];
		box.pos = label.relToMainFrame((0, 0));
		box.size = label.box.size;
	}

	override void beenFocused(HM_ZFNavEventType type) {
		switch (type) {
		case HM_ZFNavEventType_Left: tabFocus = tabLabels.size() - 1; break;

		case HM_ZFNavEventType_Right:
		case HM_ZFNavEventType_Tab:
			tabFocus = 0; break;

		case HM_ZFNavEventType_Down:
		case HM_ZFNavEventType_Up:
			tabFocus = curTab.curVal; break;
		}
	}

	private void showCorrectTab() {
		for (int i = 0; i < tabFrames.size(); i++) {
			if (i == curTab.curVal) { tabFrames[i].show(); }
			else { tabFrames[i].hide(); }
		}
	}

	/// Adds a tab to the element, with `label` displayed on the button.
	///
	/// After addition, the tab frame can be modified by getting it from
	/// [`getTabFrame`].
	void addTab(string label) {
		let button = HM_ZFRadioButton.create((0, 0), (0, 0), curTab, 0, text: label);
		let frame = HM_ZFFrame.create((0.0, tabHeight), (box.size.x, box.size.y - tabHeight));

		button.master = self;
		frame.master = self;

		elements.push(button);
		elements.push(frame);

		tabLabels.push(button);
		tabFrames.push(frame);

		setTabLabelBoxes();

		showCorrectTab();
	}

	/// Returns the tab frame at the given index.
	HM_ZFFrame getTabFrame(int index) {
		return tabFrames[index];
	}

	override void topDrawer() {
		if (curTab.curVal != lastTab) {
			lastTab = curTab.curVal;
			showCorrectTab();
		}
		Super.topDrawer();
	}

	override void drawer() {
		if (curTab.curVal != lastTab) {
			lastTab = curTab.curVal;
			showCorrectTab();
		}
		Super.drawer();
	}

	override bool onNavEvent(HM_ZFNavEventType type, bool fromController) {
		if (isFocused() && isEnabled()) {
			switch (type) {
			case HM_ZFNavEventType_Right:
				if (tabFocus != tabLabels.size() - 1) {
					tabFocus += 1;
					return true;
				}
				break;
			case HM_ZFNavEventType_Left:
				if (tabFocus != 0) {
					tabFocus -= 1;
					return true;
				}
				break;
			case HM_ZFNavEventType_Confirm:
				curTab.curVal = tabFocus;
				break;
			}
		}
		return Super.onNavEvent(type, fromController);
	}

}
